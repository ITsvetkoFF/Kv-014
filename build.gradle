// dependency versions
def spring_version = '4.2.5.RELEASE'
def hibernate_version = '5.1.0.Final'
def javax_persistence_version = '1.0.0.Final'
def postgres_version = '9.4.1208.jre7'
def hsqldb_version = '2.3.3'
def junit_version = '4.12'
def apache_commons_lang_version = '3.4'
def apache_commons_collections_version = '4.1'
def apache_commons_commons_beanutils_version = '1.9.2'
def jackson_version = '2.6.5'

def sourceVersion = '0.0.1-SNAPSHOT'

def slf4j_version = '1.7.21'
def log4j2_version = '2.5'
def slf4j_log4j2_binding_version = '2.5'
def jcl_over_slf4j_version = '1.7.21'
def jboss_logging_version = '3.3.0.Final'

def sourceComp = 1.8

ext {
    hibernate_core = "org.hibernate:hibernate-core:$hibernate_version"
    hibernate_java8 = "org.hibernate:hibernate-java8:$hibernate_version"
    javax_persistence = "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:$javax_persistence_version"
    spring_context = "org.springframework:spring-context:$spring_version"
    spring_orm = "org.springframework:spring-orm:$spring_version"
    spring_mvc = "org.springframework:spring-webmvc:$spring_version"
    spring_test = "org.springframework:spring-test:$spring_version"
    postgres_jdbc_driver = "org.postgresql:postgresql:$postgres_version"
    apache_commons_lang = "org.apache.commons:commons-lang3:$apache_commons_lang_version"
    apache_commons_collections4 = "org.apache.commons:commons-collections4:$apache_commons_collections_version"
    apache_commons_commons_beanutils = "commons-beanutils:commons-beanutils:$apache_commons_commons_beanutils_version"
    jackson = "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    junit = "junit:junit:$junit_version"
    hsqldb = "org.hsqldb:hsqldb:$hsqldb_version"

    /* Logging bridge */
    slf4j = "org.slf4j:slf4j-api:$slf4j_version"

    /* Logging framework */
    log4j2 = "org.apache.logging.log4j:log4j-core:$log4j2_version"

    /* Bridge-implementation binding */
    slf4j_log4j2_binding = "org.apache.logging.log4j:log4j-slf4j-impl:$slf4j_log4j2_binding_version"

    /* Commons-logging redirection to SLF4J. Make sure commons-logging excluded from Spring */
    jcl_over_slf4j = "org.slf4j:jcl-over-slf4j:$jcl_over_slf4j_version"

    /* Updated version of Hibernate's logger dependency. Needed for proper SLF4J integration with Hibernate */
    jboss_logging = "org.jboss.logging:jboss-logging:$jboss_logging_version"


}

allprojects {
    apply plugin: 'java'
    sourceCompatibility = sourceComp
    version = sourceVersion
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        compile slf4j, log4j2, slf4j_log4j2_binding, jcl_over_slf4j, jboss_logging
        testCompile junit
    }
    configurations {
        /* For proper SLF4J integration with Spring */
        compile.exclude group: "commons-logging"
    }
}
apply plugin: 'pmd'
pmd {
    ignoreFailures = true
    reportsDir = file("$project.buildDir/pmd/")
    ruleSetFiles = files('/pmd-ruleset.xml')
}
task pmd(type: Pmd) {
    subprojects.each() {
        source += fileTree('/' + it.name + '/src/test/java') + fileTree('/' + it.name + '/src/main/java')
    }
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
check.doLast {
    project.tasks.getByName("pmd").execute()
}
